open Std

let to_string = fun doc ->
  let buffer = IO.Buffer.create 1_024 in
  let rec write = fun ~line_start ~indent ->
    function
    | Doc.Empty ->
        line_start
    | Doc.Text value ->
        write_text ~line_start ~indent value
    | Doc.Space ->
        if line_start then
          line_start
        else (
          IO.Buffer.add_char buffer ' ';
          false
        )
    | Doc.Spaces count ->
        if line_start then
          line_start
        else (
          for _ = 1 to count do
            IO.Buffer.add_char buffer ' '
          done;
          false
        )
    | Doc.Line ->
        IO.Buffer.add_char buffer '\n';
        true
    | Doc.Break flat ->
        write ~line_start ~indent (Doc.text flat)
    | Doc.Group doc ->
        write ~line_start ~indent doc
    | Doc.Concat docs ->
        List.fold_left (fun line_start doc -> write ~line_start ~indent doc) line_start docs
    | Doc.Indent (extra, doc) ->
        write ~line_start ~indent:(indent + extra) doc
  and write_text ~line_start ~indent value =
    let rec write_lines = fun line_start is_first ->
      function
      | [] ->
          line_start
      | [ line ] ->
          if is_first && line_start && String.length line > 0 then
            IO.Buffer.add_string buffer (String.make indent ' ');
          IO.Buffer.add_string buffer line;
          line_start && String.length line = 0
      | line :: rest ->
          if is_first && line_start && String.length line > 0 then
            IO.Buffer.add_string buffer (String.make indent ' ');
          IO.Buffer.add_string buffer line;
          IO.Buffer.add_char buffer '\n';
          write_lines true false rest
    in
    write_lines line_start true (String.split_on_char '\n' value)
  in
  ignore (write ~line_start:true ~indent:0 doc);
  IO.Buffer.contents buffer
