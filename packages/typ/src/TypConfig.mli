open Std
open Model

(** Host-supplied configuration for one [Session]. *)
type env = (string * TypeScheme.t) list
type t = {
  (** Intrinsic language-level bindings visible in every source.

      This must stay small. Library/module APIs should arrive through persisted
      module summaries and host-provided ambient inputs, not by growing the
      default prelude. *)
  prelude: env;
  (** Host-loaded reusable module typings available to every source in the
      session. *)
  loaded_modules: ModuleTypings.t list;
  (** Optional semantic store used to hydrate canonical module typings during
      rooted snapshot preparation. *)
  store: Store.t option;
  (** Whether inference should retain trace payloads such as per-expression
      environments and per-item export snapshots. Hosts that only need
      diagnostics and module typings can disable this to avoid building large
      trace structures. *)
  capture_traces: bool;
  (** Snapshot-scoped bindings synthesized from sibling sources or host context. *)
  ambient: env;
  (** Snapshot-scoped lowered type declarations synthesized from summaries. *)
  ambient_type_decls: FileSummary.type_decl list;
}

(** Default host configuration used by the current prototype and tests.

    The default keeps [prelude] limited to language-level intrinsics and seeds
    a small set of bootstrap module typings through [loaded_modules]. Those
    seeded typings are a temporary stand-in for real persisted module exports
    and intentionally flow through the same [ModuleTypings] boundary that hosts
    will later hydrate from cache or build outputs. *)
val default: t

(** Replace the snapshot ambient environment while preserving the base prelude. *)
val with_ambient: t -> ambient:env -> t

(** Replace the snapshot ambient type declarations while preserving the base
    prelude, loaded modules, and ambient value environment. *)
val with_ambient_type_decls: t -> ambient_type_decls:FileSummary.type_decl list -> t

(** Replace the host-loaded reusable module typings while preserving the
    base prelude and snapshot ambient environment. *)
val with_loaded_modules: t -> loaded_modules:ModuleTypings.t list -> t

(** Replace the optional semantic store used during rooted snapshot
    preparation. *)
val with_store: t -> store:Store.t option -> t

(** Toggle retention of expression and item traces. When disabled, the checker
    still computes diagnostics and module typings, but omits trace payloads and
    leaves the type index empty. *)
val with_capture_traces: t -> capture_traces:bool -> t
