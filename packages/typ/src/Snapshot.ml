open Std

type analysis_slot = {
  source_id: SourceId.t;
  source: Source.t;
  config: TypConfig.t;
  mutable base_analysis: SourceAnalysis.t option;
  mutable analysis: SourceAnalysis.t option;
}

type t = {
  revision: int;
  analyses: analysis_slot list;
  mutable qualified_summaries: (SourceId.t * string * PersistedSummary.t) list option;
}

let make = fun ~revision ~config ~sources ->
  let analyses =
    sources
    |> List.map
      (fun (source: Source.t) ->
        {
          source_id = source.source_id;
          source;
          config;
          base_analysis = None;
          analysis = None;
        })
  in
  { revision; analyses; qualified_summaries = None }

let force_base_analysis = fun (slot: analysis_slot) ->
  match slot.base_analysis with
  | Some analysis -> analysis
  | None ->
      let analysis = SourceAnalysis.analyze ~config:slot.config slot.source in
      let () =
        slot.base_analysis <- Some analysis
      in
      analysis

let qualify_exports = fun module_name exports ->
  List.map (fun (name, scheme) -> (module_name ^ "." ^ name, scheme)) exports

let qualified_summaries = fun (snapshot: t) ->
  match snapshot.qualified_summaries with
  | Some summaries -> summaries
  | None ->
      let summaries =
        snapshot.analyses
        |> List.map
          (fun (slot: analysis_slot) ->
            let analysis = force_base_analysis slot in
            let module_name = Source.module_name slot.source in
            let persisted_summary = analysis.file_summary |> PersistedSummary.of_file_summary in
            (slot.source_id, module_name, persisted_summary))
      in
      let () =
        snapshot.qualified_summaries <- Some summaries
      in
      summaries

let ambient_env_for = fun (snapshot: t) (slot: analysis_slot) ->
  let current_module_name = Source.module_name slot.source in
  let local_modules = qualified_summaries snapshot
  |> List.filter
    (fun (candidate_source_id, _, _) -> not (SourceId.equal candidate_source_id slot.source_id))
  |> List.map
    (fun (_, module_name, summary) -> PersistedSummary.exports summary |> qualify_exports module_name) in
  let loaded_modules = slot.config.loaded_modules
  |> List.filter
    (fun summary -> not (String.equal (ModuleSummary.module_name summary) current_module_name))
  |> List.map
    (fun summary ->
      ModuleSummary.exports summary |> qualify_exports (ModuleSummary.module_name summary)) in
  List.flatten (local_modules @ loaded_modules)

let force_analysis = fun (snapshot: t) (slot: analysis_slot) ->
  match slot.analysis with
  | Some analysis -> analysis
  | None ->
      let config = TypConfig.with_ambient slot.config ~ambient:(ambient_env_for snapshot slot) in
      let analysis = SourceAnalysis.analyze ~config slot.source in
      let () =
        slot.analysis <- Some analysis
      in
      analysis

let revision = fun snapshot -> snapshot.revision

let analyses = fun snapshot -> snapshot.analyses |> List.map (force_analysis snapshot)

let file_summaries = fun snapshot ->
  analyses snapshot |> List.map (fun (analysis: SourceAnalysis.t) -> analysis.file_summary)

let persisted_summaries = fun snapshot -> file_summaries snapshot |> List.map PersistedSummary.of_file_summary

let module_summaries = fun snapshot ->
  analyses snapshot
  |> List.map
    (fun (analysis: SourceAnalysis.t) ->
      ModuleSummary.make
        ~module_name:(Source.module_name analysis.source)
        ~source_hash:(Source.input_hash analysis.source)
        ~summary:(PersistedSummary.of_file_summary analysis.file_summary))

let find_analysis = fun snapshot source_id ->
  List.find_opt
    (fun (slot: analysis_slot) ->
      SourceId.equal slot.source_id source_id)
    snapshot.analyses |> function
  | Some slot -> Some (force_analysis snapshot slot)
  | None -> None
